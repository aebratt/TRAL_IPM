

  
    model {
    #-------------------------------------------------
    # integrated population model for the Gough TRAL population
    # - age structured model with 10 age classes 
    # - adult survival based on CMR ringing data
    # - pre breeding census, female-based assuming equal sex ratio & survival
    # - productivity based on all areas incu and chick counts
    # - simplified population process with assumed age at recruiting = 10 (Wanless et al. 2009)
    # - adult breeders skipping when unsuccessful at rate of 22-32%, all successful breeders will skip
    # - linked population process with SUM OF count data
    # - v4 includes 3 scenarios of future projection: no change, improved fecundity, reduced adult survival
    # -------------------------------------------------
    
    #-------------------------------------------------  
    # 1. PRIORS FOR ALL DATA SETS
    #-------------------------------------------------
    
    
    # -------------------------------------------------        
    # 1.1. Priors and constraints FOR FECUNDITY
    # -------------------------------------------------
    
    for (t in 1:T){  
      ann.fec[t] ~ dunif(0.05,0.65) ## dnorm(0.32,10) T(0.001,0.999)        ## Informative Priors on fecundity based on Wanless et al 2009
      skip.prob[t] ~ dunif(0.05,0.95) ## dunif(0.22,0.32) T(0.001,0.999)           ## PRIOR FOR ADULT FAILED BREEDER SKIPPING PROBABILITY from Wanless et al 2009
    } #t

    
    # -------------------------------------------------        
    # 1.2. Priors and constraints FOR POPULATION COUNTS
    # -------------------------------------------------
    for (s in 1:n.sites){			### start loop over every study area
      #N.est[1,s] ~ dunif(0,400)   ## draw random value from a uniform distribution between 0 and 200 for initial population size
      sigma.proc[s] ~ dunif(0,10)	#Prior for SD of state process (annual variation in pop size)
      tau.proc[s]<-pow(sigma.proc[s],-2)
      sigma.obs[s] ~ dunif(0,100)	#Prior for SD of observation process (variation in detectability)
      tau.obs[s]<-pow(sigma.obs[s],-2)
    }


    # -------------------------------------------------        
    # 1.3. Priors and constraints FOR SURVIVAL
    # -------------------------------------------------
    
    ### RECAPTURE PROBABILITY
    mean.p.juv ~ dunif(0.2, 1)	           # Prior for mean juvenile recapture - should be higher than 20% if they survive!
    mean.p.ad ~ dunif(0.2, 1)	           # Prior for mean adult recapture - should be higher than 20%
    mu.p.juv <- log(mean.p.juv / (1-mean.p.juv)) # Logit transformation
    mu.p.ad <- log(mean.p.ad / (1-mean.p.ad)) # Logit transformation

    ## PRIORS FOR RANDOM EFFECTS
    sigma.p ~ dunif(0, 1)                # Prior for standard deviation
    tau.p <- pow(sigma.p, -2)

    
    ### SURVIVAL PROBABILITY
    mean.phi.juv ~ dunif(0, 1)             # Prior for mean juvenile survival
    mean.phi.ad ~ dunif(0.7, 1)             # Prior for mean adult survival - should be higher than 70%
    mu.juv <- log(mean.phi.juv / (1-mean.phi.juv)) # Logit transformation
    mu.ad <- log(mean.phi.ad / (1-mean.phi.ad)) # Logit transformation

    ## PRIORS FOR RANDOM EFFECTS
    sigma.phi ~ dunif(0, 1)                # Prior for standard deviation
    tau.phi <- pow(sigma.phi, -2)
    
    ## RANDOM TIME EFFECT ON SURVIVAL ONLY FOR ADULTS (age group = 2)
    for (t in 1:(n.occasions-1)){
      logit(phi.juv[t]) <- mu.juv + eps.phi[t]
      logit(phi.ad[t]) <- mu.ad + eps.phi[t]
      eps.phi[t] ~ dnorm(0, tau.phi) 
      logit(p.ad[t])  <- mu.p.ad + eps.p[t]
      logit(p.juv[t])  <- mu.p.juv + eps.p[t]
      eps.p[t] ~ dnorm(0, tau.p) 
    }


    #-------------------------------------------------  
    # 2. LIKELIHOODS AND ECOLOGICAL STATE MODEL
    #-------------------------------------------------
    
    # -------------------------------------------------        
    # 2.1. System process: female based matrix model
    # -------------------------------------------------
    
    for (tt in 2:T){
    
      ## THE PRE-BREEDING YEARS ##
    
      nestlings[tt] <- ann.fec[tt] * 0.5 * Ntot.breed[tt]                                                     ### number of locally produced FEMALE chicks
      JUV[tt] ~ dpois(nestlings[tt])                                                                     ### need a discrete number otherwise dbin will fail, dpois must be >0
      N1[tt]  ~ dbin(mean.phi.juv, round(JUV[tt-1]))                                                    ### number of 1-year old survivors 
      N2[tt] ~ dbin(mean.phi.juv, round(N1[tt-1]))                                                      ### number of 2-year old survivors
      N3[tt] ~ dbin(mean.phi.juv, round(N2[tt-1]))                                                       ### number of 3-year old survivors
      N4[tt] ~ dbin(mean.phi.juv, round(N3[tt-1]))                                                       ### number of 4-year old survivors
      N5[tt] ~ dbin(mean.phi.ad, round(N4[tt-1]))                                                       ### number of 5-year old survivors
      N6[tt] ~ dbin(mean.phi.ad, round(N5[tt-1]))                                                       ### number of 6-year old survivors
      N7[tt] ~ dbin(mean.phi.ad, round(N6[tt-1]))                                                       ### number of 7-year old survivors
      N8[tt] ~ dbin(mean.phi.ad, round(N7[tt-1]))                                                       ### number of 8-year old survivors
      N9[tt] ~ dbin(mean.phi.ad, round(N8[tt-1]))                                                       ### number of 9-year old survivors
    

      ## THE BREEDING POPULATION ##

      # Ntot.breed comprised of first-time breeders, previous skippers, and previous unsuccessful breeders
      N.succ.breed[tt] ~ dbin(ann.fec[tt], round(Ntot.breed[tt]))                     ### number of successful breeders not breeding the following year
      N.unsucc.breed[tt] <- round(Ntot.breed[tt]-N.succ.breed[tt])
      N.fail.skip[tt] ~ dbin(skip.prob[tt], max(1,N.unsucc.breed[tt]))   ### number of unsuccessful breeders not breeding the following year
      N.nonbreed[tt] <- round(sum(N.succ.breed[tt-1],N.fail.skip[tt-1])) ### number of nonbreeders as the sum of previous year successful and skipping unsuccessful breeders
      N.non.breed[tt] ~ dbin(mean.phi.ad, N.nonbreed[tt])  ### number of non-breeders in a given year is composed of the successful skippers and failed skippers from previous year       

      N.breed.ready[tt]<- round((Ntot.breed[tt-1]-N.succ.breed[tt-1]-N.fail.skip[tt-1])+N9[tt-1]+N.non.breed[tt-1])       ### number of available breeders is failed breeders from previous year plus non-breeders from previous year plus recruits (N9)
      Ntot.breed[tt] ~ dbin(mean.phi.ad, N.breed.ready[tt])   ### number of potential old breeders is the number of survivors from previous year breeders and nonbreeders

 
    } # tt
    
    
    
    ### INITIAL VALUES FOR COMPONENTS FOR YEAR 1 - based on deterministic multiplications
    Ntot.breed[1] ~ dnorm(1638,1000) #1638   ##~ dunif(1000,2000) ###   ### sum of counts is 2400, but we take average over 3 years because 2001 was an outlier year
    JUV[1]<-round(Ntot.breed[1]*0.5*ann.fec[1])
    N1[1]<-round(JUV[1]*mean.phi.juv)
    N2[1]<-round(N1[1]*mean.phi.juv)
    N3[1]<-round(N2[1]*mean.phi.juv)
    N4[1]<-round(N3[1]*mean.phi.juv)
    N5[1]<-round(N4[1]*mean.phi.ad)
    N6[1]<-round(N5[1]*mean.phi.ad)
    N7[1]<-round(N6[1]*mean.phi.ad)
    N8[1]<-round(N7[1]*mean.phi.ad)
    N9[1]<-round(N8[1]*mean.phi.ad)
    N.nonbreed[1]<-round(sum(N.succ.breed[1],N.fail.skip[1]))
    N.non.breed[1]~ dbin(mean.phi.ad, N.nonbreed[1])
    N.succ.breed[1]<- round(Ntot.breed[1]*ann.fec[1])
    N.unsucc.breed[1] <- (Ntot.breed[1]-N.succ.breed[1])
    N.fail.skip[1]<- round(N.unsucc.breed[1]*skip.prob[1])


    # -------------------------------------------------        
    # 2.2. Observation process for population counts: state-space model of annual counts
    # -------------------------------------------------
    
    for (s in 1:n.sites){			### start loop over every study area
    
      ## Observation process
    
      for (t in 1:T){
        y.count[t,s] ~ dnorm(Ntot.breed[t]*prop.sites[s], tau.obs[s])								# Distribution for random error in observed numbers (counts)
      }														# run this loop over t= nyears
    }		## end site loop

    
    # -------------------------------------------------        
    # 2.3. Likelihood for fecundity: Logistic regression from the number of surveyed broods
    # -------------------------------------------------
    for (s in 1:n.sites){			### start loop over every study area
      for (t in 1:(T-1)){
        J[t,s] ~ dpois(rho.fec[t,s])
        rho.fec[t,s] <- y.count[t,s]*ann.fec[t]
      } #	close loop over every year in which we have fecundity data
    }		## end site loop
    
    
    
    
    # -------------------------------------------------        
    # 2.4. Likelihood for adult and juvenile survival from CMR
    # -------------------------------------------------
    
    # Define the multinomial likelihood
    for (t in 1:(n.occasions-1)){
      marr.j[t,1:n.occasions] ~ dmulti(pr.j[t,], r.j[t])
      marr.a[t,1:n.occasions] ~ dmulti(pr.a[t,], r.a[t])
    }
    
    
    # Define the cell probabilities of the m-arrays
    # Main diagonal
    for (t in 1:(n.occasions-1)){
      q.juv[t] <- 1-p.juv[t]            # Probability of non-recapture
      q.ad[t] <- 1-p.ad[t]            # Probability of non-recapture
      pr.j[t,t] <- phi.juv[t]*p.juv[t]
      pr.a[t,t] <- phi.ad[t]*p.ad[t]
    
      # Above main diagonal
      for (j in (t+1):(n.occasions-1)){
        pr.j[t,j] <- phi.juv[t]*prod(phi.ad[(t+1):j])*prod(q.juv[t:(j-1)])*p.juv[j]
        pr.a[t,j] <- prod(phi.ad[t:j])*prod(q.ad[t:(j-1)])*p.ad[j]
      } #j
    
      # Below main diagonal
      for (j in 1:(t-1)){
        pr.j[t,j] <- 0
        pr.a[t,j] <- 0
      } #j
    } #t
    

    
    #-------------------------------------------------  
    # 3. DERIVED PARAMETERS FOR OUTPUT REPORTING
    #-------------------------------------------------
    

    ## DERIVED POPULATION GROWTH RATE PER YEAR
    for (t in 1:(T-1)){
      lambda[t]<-Ntot.breed[t+1]/max(1,Ntot.breed[t])  ## division by 0 creates invalid parent value
    }		## end year loop
    

    ## DERIVED MEAN FECUNDITY 
    mean.fec <- mean(ann.fec)
    mean.skip <- mean(skip.prob)
    pop.growth.rate <- exp((1/(T-1))*sum(log(lambda[1:(T-1)])))   # Geometric mean


    #-------------------------------------------------  
    # 4. PROJECTION INTO FUTURE
    #-------------------------------------------------
    ## includes 3 scenarios
    ## scenario 1: projection with no changes in demography
    ## scenario 2: successful mouse eradication in 2021 - fecundity doubles
    ## scenario 3: increasing mouse impacts on adult survival (adult survival decreases by 10%)


for(scen in 1:n.scenarios){


    ### COPY POPULATIONS FROM LAST YEAR OF DATA SERIES

    nestlings.f[scen,1] <- round(fut.fec.change[scen]*mean.fec* 0.5 * Ntot.breed[T])                                             ### number of locally produced FEMALE chicks based on average fecundity - to use just one take ann.fec[FUT.int[tt]] 
    N1.f[scen,1]  ~ dbin(mean.phi.juv, max(1,round(nestlings[T])))                                                    ### number of 1-year old survivors 
    N2.f[scen,1] ~ dbin(mean.phi.juv, max(1,round(N1[T])))                                                      ### number of 2-year old survivors
    N3.f[scen,1] ~ dbin(mean.phi.juv, max(1,round(N2[T])))                                                       ### number of 3-year old survivors
    N4.f[scen,1] ~ dbin(mean.phi.juv, max(1,round(N3[T])))                                                       ### number of 4-year old survivors
    N5.f[scen,1] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N4[T])))                                                       ### number of 5-year old survivors
    N6.f[scen,1] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N5[T])))                                                       ### number of 6-year old survivors
    N7.f[scen,1] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N6[T])))                                                       ### number of 7-year old survivors
    N8.f[scen,1] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N7[T])))                                                       ### number of 8-year old survivors
    N9.f[scen,1] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N8[T])))                                                       ### number of 9-year old survivors
    N.succ.breed.f[scen,1] ~ dbin(fut.fec.change[scen]*mean.fec, max(1,round(Ntot.breed[T])))                     ### number of successful breeders not breeding the following year
    N.unsucc.breed.f[scen,1] <- round(Ntot.breed[T]-N.succ.breed[T])
    N.fail.skip.f[scen,1] ~ dbin(mean.skip, max(1,N.unsucc.breed[T]))   ### number of unsuccessful breeders not breeding the following year
    N.nonbreed.f[scen,1] <- round(sum(N.succ.breed[T-1],N.fail.skip[T-1])) ### number of nonbreeders as the sum of previous year successful and skipping unsuccessful breeders
    N.non.breed.f[scen,1] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,N.nonbreed[T]))  ### number of non-breeders in a given year is composed of the successful skippers and failed skippers from previous year       
    N.breed.ready.f[scen,1]<- round((Ntot.breed[T-1]-N.succ.breed[T-1]-N.fail.skip[T-1])+N9[T-1]+N.non.breed[T-1])       ### number of available breeders is failed breeders from previous year plus non-breeders from previous year plus recruits (N9)
    Ntot.breed.f[scen,1] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,N.breed.ready[T]))   ### number of potential old breeders is the number of survivors from previous year breeders and nonbreeders
    carr.capacity[scen,1]<-2500
    Ntot.breed.raw[scen,1]<-Ntot.breed.f[scen,1]

   
    for (tt in 2:FUT.YEAR){
    

    # -------------------------------------------------        
    # 4.1. System process for future
    # -------------------------------------------------

    ## INCLUDE CARRYING CAPACITY OF 2500 breeding pairs (slightly more than maximum ever counted)
    carr.capacity[scen,tt] ~ dnorm(2500,100)
    
    ## THE PRE-BREEDING YEARS ##
    ## because it goes for 30 years, all pops must be safeguarded to not become 0 because that leads to invald parent error
    
    nestlings.f[scen,tt] <- round(fut.fec.change[scen]*mean.fec* 0.5 * Ntot.breed.f[scen,tt])                       ### number of locally produced FEMALE chicks based on average fecundity - to use just one take ann.fec[FUT.int[tt]] 
    N1.f[scen,tt]  ~ dbin(mean.phi.juv, max(1,round(nestlings.f[scen,tt])))                                              ### number of 1-year old survivors 
    N2.f[scen,tt] ~ dbin(mean.phi.juv, max(1,round(N1.f[scen,tt])))                                                      ### number of 2-year old survivors
    N3.f[scen,tt] ~ dbin(mean.phi.juv, max(1,round(N2.f[scen,tt])))                                                       ### number of 3-year old survivors
    N4.f[scen,tt] ~ dbin(mean.phi.juv, max(1,round(N3.f[scen,tt])))                                                       ### number of 4-year old survivors
    N5.f[scen,tt] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N4.f[scen,tt])))                                                       ### number of 5-year old survivors
    N6.f[scen,tt] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N5.f[scen,tt])))                                 ### number of 6-year old survivors
    N7.f[scen,tt] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N6.f[scen,tt])))                                 ### number of 7-year old survivors
    N8.f[scen,tt] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N7.f[scen,tt])))                                 ### number of 8-year old survivors
    N9.f[scen,tt] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,round(N8.f[scen,tt-1])))                               ### number of 9-year old survivors
    

    ## THE BREEDING POPULATION ##

    # Ntot.breed comprised of first-time breeders, previous skippers, and previous unsuccessful breeders
    N.succ.breed.f[scen,tt] ~ dbin(fut.fec.change[scen]*mean.fec, max(1,round(Ntot.breed.f[scen,tt])))                     ### number of successful breeders not breeding the following year
    N.unsucc.breed.f[scen,tt] <- round(Ntot.breed.f[scen,tt]-N.succ.breed.f[scen,tt])
    N.fail.skip.f[scen,tt] ~ dbin(mean.skip, max(1,N.unsucc.breed.f[scen,tt]))   ### number of unsuccessful breeders not breeding the following year
    N.nonbreed.f[scen,tt] <- round(sum(N.succ.breed.f[scen,tt-1],N.fail.skip.f[scen,tt-1])) ### number of nonbreeders as the sum of previous year successful and skipping unsuccessful breeders
    N.non.breed.f[scen,tt] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,N.nonbreed.f[scen,tt]))  ### number of non-breeders in a given year is composed of the successful skippers and failed skippers from previous year       
    
    N.breed.ready.f[scen,tt]<- round((Ntot.breed.f[scen,tt-1]-N.succ.breed.f[scen,tt-1]-N.fail.skip.f[scen,tt-1])+N9.f[scen,tt-1]+N.non.breed.f[scen,tt-1])       ### number of available breeders is failed breeders from previous year plus non-breeders from previous year plus recruits (N9)
    Ntot.breed.raw[scen,tt] ~ dbin(fut.surv.change[scen]*mean.phi.ad, max(1,N.breed.ready.f[scen,tt]))   ### number of potential old breeders is the number of survivors from previous year breeders and nonbreeders
    Ntot.breed.f[scen,tt] <- min(Ntot.breed.raw[scen,tt],carr.capacity[scen,tt])  ## simply reduces number of breeders to carrying capacity

    } ### end future loop
    
    ## CALCULATE ANNUAL POP GROWTH RATE ##
      for (fut2 in 1:(FUT.YEAR-1)){
        fut.lambda[scen,fut2] <- Ntot.breed.f[scen,fut2+1]/max(1,Ntot.breed.f[scen,fut2])                                 ### inserted safety to prevent denominator being 0
      } # fut2
    

    ## DERIVED MEAN FUTURE GROWTH RATE 
    fut.growth.rate[scen] <- exp((1/(FUT.YEAR-1))*sum(log(fut.lambda[scen,1:(FUT.YEAR-1)])))   # Geometric mean

} # end future projection scenarios
    
}  ## end model loop

